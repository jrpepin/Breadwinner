*-------------------------------------------------------------------------------
* BREADWINNER PROJECT - SIPP14 Component
* sipp14_repeatBW_hh60.do
* Kelly Raley and Joanna Pepin
*-------------------------------------------------------------------------------

********************************************************************************
* DESCRIPTION
********************************************************************************
* The purpose of this analysis is to account for repeat bw unobserved in the SIPP
* using the observed bw estimates generated from the NLSY

* This file will only work after breadwinnerNLSY97 AND breadwinnerSIPP14 files
* have been run in the same Stata session.

********************************************************************************
* Calculate the "Repeat BW discount"
********************************************************************************
// Display the macros of censored bw by duraiton from the NLSY analysis
* Note, these macros were generated by 04_nlsy97_bw_estimates_hh60

	di %9.2f $NLSY60_t5
	di %9.2f $NLSY60_t6
	di %9.2f $NLSY60_t7

// Display the macros of censored bw by duraiton from the SIPP analysis
* Note, these macros were generated by 08_sipp14_bw_estimates_hh60

	di %9.2f $SIPP60_t5
	di %9.2f $SIPP60_t6
	di %9.2f $SIPP60_t7
	
// Calculate the ratio of NLSY transition rate to SIPP transition rate for durations 5-7
* A graph of the transition rates shows 5-7 is where the estimates begin to deviate

	di %9.2f $NLSY60_t5/$SIPP60_t5
	di %9.2f $NLSY60_t6/$SIPP60_t6
	di %9.2f $NLSY60_t7/$SIPP60_t7

// Calculate the average ratio for the three years
	global discount60 = (($NLSY60_t5/$SIPP60_t5) + ($NLSY60_t6/$SIPP60_t6) + ($NLSY60_t7/$SIPP60_t7))/3
	di %9.2f $discount60

********************************************************************************
* Apply the "Repeat BW discount" for years 5 - 17
********************************************************************************

* initialize new cumulative measure at birth -----------------------------------
cap drop		notbw60adj_*
	gen 		notbw60adj_0 		= 1 - (.01*$per_bw60_atbirth)
	gen     	notbw60adj_lesshs 	= (1-prop_bw60_atbirth1)
	gen     	notbw60adj_hs      	= (1-prop_bw60_atbirth2)
	gen     	notbw60adj_somecol 	= (1-prop_bw60_atbirth3)
	gen     	notbw60adj_univ   	= (1-prop_bw60_atbirth4)

* the proportion who do not become bw ------------------------------------------

// These stay the same
	forvalues a=1/4 {
		gen notbw60adj_`a'      	= 1 - firstbw60_`a'[1,2]
		gen notbw60adj_lesshs_`a' 	= 1 - firstbw601_`a'[1,2]
		gen notbw60adj_hs_`a' 	   	= 1 - firstbw602_`a'[1,2]
		gen notbw60adj_somecol_`a' 	= 1 - firstbw603_`a'[1,2]
		gen notbw60adj_univ_`a' 	= 1 - firstbw604_`a'[1,2]
	}
	
// These get the repeat bw discount
	forvalues b=5/17 {
		gen notbw60adj_`b' 			= 1 - firstbw60_`b'[1,2] 	* $discount60
		gen notbw60adj_lesshs_`b' 	= 1 - firstbw601_`b'[1,2]	* $discount60
		gen notbw60adj_hs_`b' 	   	= 1 - firstbw602_`b'[1,2]	* $discount60
		gen notbw60adj_somecol_`b' 	= 1 - firstbw603_`b'[1,2]	* $discount60
		gen notbw60adj_univ_`b' 	= 1 - firstbw604_`b'[1,2]	* $discount60		
	}

* Create the total macros (adjusted bw) _---------------------------------------
	global adj_60_0 = .01*$per_bw60_atbirth

	forvalues d=1/4 {
		global adj60_`d' = firstbw60_`d'[1,2]
	forvalues d=5/17 {
		global adj60_`d' = firstbw60_`d'[1,2] * $discount60
	}
	}
	
* Calculate adjusted survival rates --------------------------------------------

cap drop sur_*
	gen  sur_0        	= 	notbw60adj_0
	
	gen  sur_lesshs_0  	=  	notbw60adj_lesshs
	gen  sur_hs_0     	=  	notbw60adj_hs
	gen  sur_somecol_0  = 	notbw60adj_somecol
	gen  sur_univ_0   	=  	notbw60adj_univ

forvalues d=1/17 {
	local lag = `d'-1
	gen sur_`d' 	  	= (sur_`lag')       	* (notbw60adj_`d')
	
	gen sur_lesshs_`d' 	= (sur_lesshs_`lag') 	* (notbw60adj_lesshs_`d')
	gen sur_hs_`d' 		= (sur_hs_`lag')    	* (notbw60adj_hs_`d')
	gen sur_somecol_`d' = (sur_somecol_`lag')	* (notbw60adj_somecol_`d')
	gen sur_univ_`d' 	= (sur_univ_`lag')  	* (notbw60adj_univ_`d')
}

********************************************************************************
* Put results in an excel file
********************************************************************************

// Create Shell
putexcel set "$output/Descriptives60.xlsx", sheet(proportions) modify
putexcel A8 = ("SIPP Adjusted (18 yrs)")

// ADJUSTED BW by age 18
putexcel B8 = (100*(1-sur_17))  			, nformat(number_d2) // Total
putexcel C8 = (100*(1-sur_lesshs_17))  		, nformat(number_d2) // < HS
putexcel D8 = (100*(1-sur_hs_17))  			, nformat(number_d2) // HS
putexcel E8 = (100*(1-sur_somecol_17))  	, nformat(number_d2) // Some col
putexcel F8 = (100*(1-sur_univ_17))  		, nformat(number_d2) // College
