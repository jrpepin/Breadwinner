*-------------------------------------------------------------------------------
* BREADWINNER PROJECT - SIPP14 Component
* sipp14_repeatBW_hh60.do
* Kelly Raley and Joanna Pepin
*-------------------------------------------------------------------------------

********************************************************************************
* DESCRIPTION
********************************************************************************
* The purpose of this analysis is to account for repeat bw unobserved in the SIPP
* using the observed bw estimates generated from the NLSY

* This file will only work after breadwinnerNLSY97 AND breadwinnerSIPP14 files
* have been run in the same Stata session.

********************************************************************************
* Calculate the "Repeat BW discount"
********************************************************************************
// Display the macros of censored bw by duraiton from the NLSY analysis
* Note, these macros were generated by 04_nlsy97_bw_estimates_hh60

	di %9.2f $NLSY60_t5
	di %9.2f $NLSY60_t6
	di %9.2f $NLSY60_t7

// Display the macros of censored bw by duraiton from the SIPP analysis
* Note, these macros were generated by 10_sipp14_bw_estimates_hh60

	di %9.2f $SIPP60_t5
	di %9.2f $SIPP60_t6
	di %9.2f $SIPP60_t7
	
// Calculate the ratio of NLSY transition rate to SIPP transition rate for durations 5-7
* A graph of the transition rates shows 5-7 is where the estimates begin to deviate

	di %9.2f $NLSY60_t5/$SIPP60_t5
	di %9.2f $NLSY60_t6/$SIPP60_t6
	di %9.2f $NLSY60_t7/$SIPP60_t7

// Calculate the average ratio for the three years
	global discount60 = (($NLSY60_t5/$SIPP60_t5) + ($NLSY60_t6/$SIPP60_t6) + ($NLSY60_t7/$SIPP60_t7))/3
	di %9.2f $discount60

local raceth "white black asian other hispanic"
local ed "lesshs hs somecol univ"
local nmbst "marital nonmar"

********************************************************************************
* Apply the "Repeat BW discount" for years 5 - 17
********************************************************************************

* initialize new cumulative measure at birth -----------------------------------
cap drop		notbw60adj_*
gen 		notbw60adj_0 		= 1 - (.01*$per_bw60_atbirth)
gen     	notbw60adj_lesshs 	= (1-prop_bw60_atbirth1)
gen     	notbw60adj_hs      	= (1-prop_bw60_atbirth2)
gen     	notbw60adj_somecol 	= (1-prop_bw60_atbirth3)
gen     	notbw60adj_univ   	= (1-prop_bw60_atbirth4)
gen     	notbw60adj_marital  	= (1-prop_bw60_atbirth_nmb0)
gen     	notbw60adj_nonmar   	= (1-prop_bw60_atbirth_nmb1)

gen     	notbw60adj_white    = (1-prop_bw60_atbirth_r1)
gen     	notbw60adj_black 	= (1-prop_bw60_atbirth_r2)
gen     	notbw60adj_asian   	= (1-prop_bw60_atbirth_r3)
gen     	notbw60adj_other   	= (1-prop_bw60_atbirth_r4)
gen     	notbw60adj_hispanic = (1-prop_bw60_atbirth_r5)

foreach re in 1 2 5{
	local race : word `re' of `raceth'
	gen     	notbw60adj_lesshs_`race' 	= (1-prop_bw60_atbirth1_`race')
	gen     	notbw60adj_hs_`race'      	= (1-prop_bw60_atbirth2_`race')
	gen     	notbw60adj_somecol_`race' 	= (1-prop_bw60_atbirth3_`race')
	gen     	notbw60adj_univ_`race'   	= (1-prop_bw60_atbirth4_`race')
}

* the proportion who do not become bw ------------------------------------------

// These stay the same
forvalues d=1/4 {
 
    gen notbw60adj_`d' 	= 1 - firstbw60_`d'[1,2]   
	
	// by education
	forvalues e=1/4{
		local educ : word `e' of `ed'
		gen notbw60adj_`educ'_`d' 	= 1 - firstbw60`e'_`d'[1,2]
	// by education by race
		foreach re in 1 2 5{
			local race: word `re' of `raceth'
			gen notbw60adj_`educ'_`race'_`d' = 1-firstbw60`e'_`race'_`d'[1,2]
		}
	}
	
	// by race
	forvalues r=1/5{
		local race : word `r' of `raceth'
		gen notbw60adj_`race'_`d' 	= 1 - firstbw60_r`r'_`d'[1,2]
	}
	
	// by marital status at first birth
		forvalues fb=0/1 {
			local nextword = `fb' + 1
			local nmb: word `nextword' of `nmbst'
			gen notbw60adj_`nmb'_`d' = 1 - firstbw60_nmb`fb'_`d'[1,2]
		}
}
	

	
// These get the repeat bw discount
forvalues d=5/17 {
	gen notbw60adj_`d' 		= 1 - firstbw60_`d'[1,2] 	* $discount60
	
	// by education
	forvalues e=1/4{
		local educ : word `e' of `ed'
		gen notbw60adj_`educ'_`d' 	= 1 - firstbw60`e'_`d'[1,2] * $discount60
	// by education by race
		foreach re in 1 2 5{
			local race: word `re' of `raceth'
			gen notbw60adj_`educ'_`race'_`d' = 1-firstbw60`e'_`race'_`d'[1,2] * $discount60
		}
	}	
	
	// by race
	forvalues r=1/5{
		local race : word `r' of `raceth'
		gen notbw60adj_`race'_`d' 	= 1 - firstbw60_r`r'_`d'[1,2] * $discount60
	}
	
	// by marital status at first birth
		forvalues fb=0/1 {
			local nextword = `fb' + 1
			local nmb: word `nextword' of `nmbst'
			gen notbw60adj_`nmb'_`d' = 1 - firstbw60_nmb`fb'_`d'[1,2]*$discount60
		}
}

* Create the total macros (adjusted bw) _---------------------------------------
global adj_60_0 = .01*$per_bw60_atbirth

forvalues d=1/4 {
	global adj60_`d' = firstbw60_`d'[1,2]
}
forvalues d=5/17 {
	global adj60_`d' = firstbw60_`d'[1,2] * $discount60
}
	
* Calculate adjusted survival rates --------------------------------------------

cap drop sur_*
	gen  sur_0        	= 	notbw60adj_0
	
	// by education
	forvalues e=1/4{
		local educ : word `e' of `ed'
		gen  sur_`educ'_0  	=  	notbw60adj_`educ'
	// by education by race	
		foreach re in 1 2 5{
			local race : word `re' of `raceth'
			gen  sur_`educ'_`race'_0  	=  	notbw60adj_`educ'_`race'
		}
	}
		
	// by race
	forvalues r=1/5{
		local race : word `r' of `raceth'
		gen  sur_`race'_0  	=  	notbw60adj_`race'
	}
		
	// by marital status at first birth
		forvalues fb=0/1 {
			local nextword = `fb' + 1
			local nmb: word `nextword' of `nmbst'
			gen sur_`nmb'_0 = notbw60adj_`nmb'
		}

forvalues d=1/17 {
	local lag = `d'-1
	gen sur_`d' 	  	= (sur_`lag')       	* (notbw60adj_`d')
	
	// by education
	forvalues e=1/4{
		local educ : word `e' of `ed'
		gen sur_`educ'_`d' 	= (sur_`educ'_`lag') 	* (notbw60adj_`educ'_`d')
		foreach re in 1 2 5{
			local race : word `re' of `raceth'
			gen sur_`educ'_`race'_`d' 	= (sur_`educ'_`race'_`lag') 	* (notbw60adj_`educ'_`race'_`d')
		}
	}
	// by race
	forvalues r=1/5{
		local race : word `r' of `raceth'
		gen sur_`race'_`d' 	= (sur_`race'_`lag') 	* (notbw60adj_`race'_`d')
	}
	// by marital status at first birth
		forvalues fb=0/1 {
			local nextword = `fb' + 1
			local nmb: word `nextword' of `nmbst'
			gen sur_`nmb'_`d' = (sur_`nmb'_`lag') *  (notbw60adj_`nmb'_`d')
		}
}

********************************************************************************
* Put results in an excel file
********************************************************************************

// Create Shell
putexcel set "$output/Descriptives60.xlsx", sheet(proportions) modify
* Note that parts of this tables are written in the previous file (10_sipp14_bw_estimates_hh60.do)
putexcel A8 = ("SIPP Adjusted (18 yrs)")

// ADJUSTED BW by age 18
putexcel B8 = (100*(1-sur_17))  		, nformat(number_d2) // Total
putexcel C8 = (100*(1-sur_lesshs_17))  		, nformat(number_d2) // < HS
putexcel D8 = (100*(1-sur_hs_17))  		, nformat(number_d2) // HS
putexcel E8 = (100*(1-sur_somecol_17))  	, nformat(number_d2) // Some col
putexcel F8 = (100*(1-sur_univ_17))  		, nformat(number_d2) // College
putexcel U8 = (100*(1-sur_white_17))  		, nformat(number_d2) // White
putexcel V8 = (100*(1-sur_black_17))  		, nformat(number_d2) // Black
putexcel W8 = (100*(1-sur_asian_17))  		, nformat(number_d2) // Asian
putexcel X8 = (100*(1-sur_other_17))  		, nformat(number_d2) // Other
putexcel Y8 = (100*(1-sur_hispanic_17))  	, nformat(number_d2) // Hispanic

local columns "G H I J K L M N O P Q R"

local c = 1

foreach re in 1 2 5{
	local race : word `re' of `raceth'
	forvalues e=1/4{
		local col : word `c' of `columns'
		local educ : word `e' of `ed'
		putexcel `col'8 = (100*(1-sur_`educ'_`race'_17)), nformat(number_d2)
		local c = `c' + 1
	}
}

local columns "S T"

forvalues fb = 0/1 {
	local nextword = `fb' + 1
	local nmb: word `nextword' of `nmbst'
	local col: word `nextword' of `columns'
	putexcel `col'8 = (100*(1-sur_`nmb'_17)), nformat(number_d2)
}

	
			
